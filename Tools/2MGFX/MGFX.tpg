// MonoGame Effect Parser
//
// For use with "Tiny Parser Generator"
// http://www.codeproject.com/Articles/28294/a-Tiny-Parser-Generator-v1-2
//

<% @TinyPG Namespace="TwoMGFX" Language="C#"%>

[Skip] BlockComment -> @"/\*([^*]|\*[^/])*\*/";
[Skip] Comment -> @"//[^\n\r]*";
[Skip] Whitespace -> @"[ \t\n\r]+";

// TOKENS
Identifier -> @"[A-Za-z_][A-Za-z0-9_]*";
Pass -> @"pass";
Technique -> @"technique";
VertexShader -> @"VertexShader";
PixelShader -> @"PixelShader";
OpenBracket -> @"{";
CloseBracket -> @"}";
Equals -> @"=";
Semicolon -> @";";
OpenParenthesis -> @"\(";
CloseParenthesis -> @"\)";
Compile -> @"compile";
ShaderModel -> @"(vs_|ps_)(2_0|3_0|4_0|5_0)((_level_)(9_1|9_2|9_3))?";
Code -> @".";
EndOfFile -> @"^$";

// Productions
Start -> ( Code | Technique_Declaration )* EndOfFile
{ 
   ShaderInfo shader = new ShaderInfo();

   foreach (ParseNode node in Nodes)
   {
      var technique = node.Eval(tree, shader) as TechniqueInfo;
      if ( technique != null )
         shader.Techniques.Add(technique);
   }

   return shader; 
};

Technique_Declaration -> Technique Identifier? OpenBracket Pass_Declaration+ CloseBracket 
{
   TechniqueInfo technique = new TechniqueInfo();
   technique.name = $Identifier as string ?? string.Empty;
   technique.startPos = Token.StartPos;
   technique.length = Token.Length;

   foreach (ParseNode node in Nodes)
   {
      var pass = node.Eval(tree, technique) as PassInfo;
      if ( pass != null )
         technique.Passes.Add(pass);
   }

   return technique;
};

Pass_Declaration ->	Pass Identifier? OpenBracket (VertexShader_Pass_Expression | PixelShader_Pass_Expression)* CloseBracket 
{ 
   PassInfo pass = new PassInfo();
   pass.name = $Identifier as string ?? string.Empty;

   foreach (ParseNode node in Nodes)
      node.Eval(tree, pass);

   return pass;
};

VertexShader_Pass_Expression -> VertexShader Equals Compile ShaderModel Identifier OpenParenthesis CloseParenthesis Semicolon
{
   PassInfo pass = paramlist[0] as PassInfo;
   pass.vsModel = $ShaderModel as string;
   pass.vsFunction = $Identifier as string;
   return null;
};

PixelShader_Pass_Expression -> PixelShader Equals Compile ShaderModel Identifier OpenParenthesis CloseParenthesis Semicolon
{
   PassInfo pass = paramlist[0] as PassInfo;
   pass.psModel = $ShaderModel as string;
   pass.psFunction = $Identifier as string;
   return null;
};

